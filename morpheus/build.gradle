apply plugin: 'com.android.library'
apply plugin: 'maven-publish'


android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 26
        versionCode 20
        versionName "0.6.12"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFile 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.10'
    testImplementation 'org.json:json:20160810'
    testImplementation 'org.robolectric:robolectric:3.4.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'

    api 'com.google.code.gson:gson:2.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.6.0'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        androidsdk(MavenPublication) {
            groupId 'com.healthtap.androidsdk'
            artifactId 'morpheus'
            version android.defaultConfig.versionName
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/morpheus-release.aar")
        }
    }
    repositories {
        maven {
            url "s3://healthtap-android.s3.amazonaws.com/repo"
            credentials(AwsCredentials) {
                if (System.getenv("TRAVIS_CI")) {

                } else {
                    accessKey AWS_ACCESS_KEY
                    secretKey AWS_SECRET_KEY
                }
            }
        }
    }
}

//apply from: 'https://raw.githubusercontent.com/xamoom-raphael/JCenter/master/installv1.gradle'
//apply from: 'https://raw.githubusercontent.com/xamoom-raphael/JCenter/master/bintrayv1.gradle'
