apply plugin: 'com.android.library'
apply plugin: 'maven-publish'


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 6
        versionName "0.5.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.10.10'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.10'

    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        androidsdk(MavenPublication) {
            groupId 'com.healthtap.androidsdk'
            artifactId 'morpheus'
            version android.defaultConfig.versionName
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/morpheus-release.aar")
        }
    }
    repositories {
        maven {
            url "s3://healthtap-android.s3.amazonaws.com/repo"
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY
                secretKey AWS_SECRET_KEY
            }
        }
    }
}

//apply from: 'https://raw.githubusercontent.com/xamoom-raphael/JCenter/master/installv1.gradle'
//apply from: 'https://raw.githubusercontent.com/xamoom-raphael/JCenter/master/bintrayv1.gradle'